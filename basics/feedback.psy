# the following is an example of a Simon task with feedback about
# error rates and response times

options
  executable feedback

bitmaps
  left
  right
  fix

fonts
  arial 18

table simontasktable
  "L_pos L_arrow" -200 1 left   1
  "R_pos R_arrow"  200 2 right  2
  "R_pos L_arrow"  200 3 left   1
  "L_pos R_arrow" -200 4 right  2

task intro

task simon
  clear screen
  table simontasktable
  keys a l
  show bitmap fix
  delay 100
  show bitmap @4 @2 0
  readkey @5 2000
  delay 3000
  save BLOCKNAME @1 @3 TABLEROW KEY STATUS RT

block mytrainingblock
  font arial
  show text "Use the key 'a' for left and 'l' for right"
  delay 3000
  tasklist
    simon 3
  end
  feedback
    set &MyPerc perc     ; select c7 != 1
    set &MyMean mean c8  ; select c7 == 1
    set &MyCount count   ; select c7 == 1
    text 0 -250  "Error percentage and response times in training block"
    text 0 -200  "(in ms = milliseconds = there are 1000 ms in one second)"
    text 0 -150 &MyPerc  ; prefix "Percentage errors (should really be below 10pct):" ; postfix "%"
    text 0 -100 &MyMean  ; prefix "Average speed of ALL CORRECT RESPONSES:" ; postfix "ms"
    text 0 -50  &MyCount ; prefix "Total number of correct trials:"
    text 0  200  "This was just training. Now press space bar of your keyboard"
    text 0  250  "with the REAL data collection (will take about 5 minutes)"
  end
