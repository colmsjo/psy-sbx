# dentol.psy
#
# 181216, Jonas Colmsj√∂
#
# Study of strong vs weak attributes in a high vs low elaboration setting.
#
# The data file has the following columns
# &elabScenario TASKNAME TABLEROW TRIALCOUNT RT &falseTrue   - for adjective
# &elabScenario TASKNAME TABLEROW TRIALCOUNT RT &dislikeLike - for statements
#
# Statements are used to test Attribute judgements
# Adjectives are used to test Evaluative judgements
#


options
  executable dentol
  set &reactTime    0
  set &dislikeLike  0
  set &falseTrue    0
  set &elabScenario 0

bitmaps
  selector

fonts
  arial 18

#
# tables
# -------

table elabInstr
  "Grupp 1."                                # set &elabScenario to 1
  "Grupp 2."                                # set &elabScenario to 2

table dentolStatements
  "Dentol toothpaste fights gum disease."                         # row1
  "Dentol toothpaste kills germs that cause bad breath."          # row2
  "Dentol toothpaste prevents cavities."                          # row3

table dentolAdjectives
  "Dentol toothpaste." "Dislike"  "Like"
  "Dentol toothpaste." "Positive" "Negative"
  "Dentol toothpaste." "Good"     "Bad"

table coverStatements
  "Pepsodent gives whiter teath."
  "Pepsodent improves you mouth health."
  "Colgate prevents tartar and plaque"

table coverAdjectives
  "Pepsodent." "Dislike"  "Like"
  "Pepsodent." "Positive" "Negative"
  "Colgate."   "Good"     "Bad"

#
# tasks
# -------

task showIntro
  font arial
  show text "We are investigating how attitudes towards new brands are created in"      0 -160
  show text "relatioship towards existing brands. Each part of the study will have"     0 -120
  show text "instructions. Please read the instructions and to follow them as "         0 -80
  show text "carefully as you can. The study will take approx. 5 minutes."              0 -40
  show text "Thank you for your participation!"                                         0  40
  show text "(Click to continue)"                                                       0  80
  readmouse l 1 60000

task elabInstr
  table elabInstr
  font arial
  show text "(Click to continue)"                                   0 200
  set &elabScenario TABLEROW                              # 1=low, 2=high
  if &elabScenario == 1
    show text "Read the statements on next page."                    0 -120
    show text "Count the vowels on each row."                       0 -80
    show text "Multiply the number with the number of the row."     0 -40
    show text "Add the numbers. Do not write anything down."        0  0
  fi
  if &elabScenario == 2
    show text "Read the statement on next page."                     0 -80
    show text "Pay attention to the thougt you have for the texts."  0 -40
    show text "Take as much time as you need."                       0  0
  fi
  readmouse l 1 60000

task coverInstr
  font arial
  show text "Answer the following statements as quickly as possible."  0   0
  delay 5000

task dentolInstr
  font arial
  show text "Answer the following statements as quickly as possible."  0   0
  delay 5000

task thankYouMessage
  font arial
  show text "Thank you for your participation!"  0   0
  delay 2000

task dentolMessage
  font arial
  show text "Dentol toothpaste."                 0 -80
  show text "Fights gum disease."                0   0
  show text "Kills germs that cause bad breath." 0  40
  show text "Prevents cavities."                 0  80
  show text "(Click to continue)"       0 200
  readmouse l 1 120000

task coverStatements
  table coverStatements
  clear screen
  mouse show
  show bitmap selector           -100  40
  show bitmap selector            100  40
  show text @1                      0 -80
  show text "False"             -100  40
  show text "True"                100  40
  readmouse l 1 10000                                   # l=left mouse button, 1=first stiumli correct, 5000=wait for 5secs
  set $mouseX MOUSE_X                                   # current mouse x-coordinate
  set $mouseY MOUSE_Y                                   # current mouse x-coordinate
  set &falseTrue bitmap-under-mouse $mouseX $mouseY     # 1=Fasle, 2=True
  set &reactTime RT

task coverAdjectives
  table coverAdjectives
  clear screen
  mouse show
  show bitmap selector           -100  40
  show bitmap selector            100  40
  show text @1                      0 -80
  show text @2                   -100  40
  show text @3                    100  40
  readmouse l 1 10000
  set $mouseX MOUSE_X
  set $mouseY MOUSE_Y
  set &dislikeLike bitmap-under-mouse $mouseX $mouseY
  set &reactTime RT

task dentolStatements
  table dentolStatements
  clear screen
  mouse show
  show bitmap selector           -100  40
  show bitmap selector            100  40
  show text @1                      0 -80
  show text "False"             -100  40
  show text "True"                100  40
  readmouse l 1 10000
  set $mouseX MOUSE_X
  set $mouseY MOUSE_Y
  set &dislikeLike bitmap-under-mouse $mouseX $mouseY
  set &reactTime RT
  save &elabScenario TASKNAME TABLEROW TRIALCOUNT RT &dislikeLike

task dentolAdjectives
  table dentolAdjectives
  clear screen
  mouse show
  show bitmap selector           -100  40
  show bitmap selector            100  40
  show text @1                      0 -80
  show text @2                   -100  40
  show text @3                    100  40
  readmouse l 1 10000
  set $mouseX MOUSE_X
  set $mouseY MOUSE_Y
  set &falseTrue bitmap-under-mouse $mouseX $mouseY
  set &reactTime RT
  save &elabScenario TASKNAME TABLEROW TRIALCOUNT RT &falseTrue

#
# blocks
# -------

block intro
  tasklist
    showIntro 1
  end

block instruction
  tasklist
    elabInstr 1
  end

block dentolMessag
  tasklist
    dentolMessage 1
  end

block coverInstr
  tasklist
    coverInstr 1
  end

block coverStatementsAdjectives
  tasklist
    coverStatements 3 fixed
    coverAdjectives 3 fixed
  end

block dentolInstr
  tasklist
    dentolInstr 1
  end

# Evalualtive judgement
block dentolAdjectives
  tasklist
    dentolAdjectives 3 fixed
  end

# Attribute judgements
block dentolStatements
  tasklist
    dentolStatements 3 fixed
  end

block thankYouMessage
  tasklist
    thankYouMessage 1
  end
