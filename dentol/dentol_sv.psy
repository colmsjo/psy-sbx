# dentol.psy
#
# 181216, Jonas Colmsjö
#
# Study of strong vs weak attributes in a high vs low elaboration setting.
#
# The data file has the following columns
# &elabScenario TASKNAME TABLEROW TRIALCOUNT RT &falseTrue   - for adjective
# &elabScenario TASKNAME TABLEROW TRIALCOUNT RT &dislikeLike - for statements


options
  executable dentol
  set &reactTime    0
  set &dislikeLike  0
  set &falseTrue    0
  set &elabScenario 0

bitmaps
  intro
  selector

fonts
  arial 18

#
# tables
# -------

table elabInstr
  "Grupp 1."  # set &elabScenario to 1
  "Grupp 2." # set &elabScenario to 2

table dentolStatements
  "Dentol tandkräm bekämpar tandköttsfinflammation."              # row1
  "Dentol tandkräm ger en bättre andedräkt."                      # row2
  "Dentol tandkräm förebygger karies."                            # row3

table dentolAdjectives
  "Dentol tandkräm." "Gillar inte"  "Gillar"
  "Dentol tandkräm." "Negativ" "Positiv"
  "Dentol tandkräm." "Dålig"     "Bra"

table coverStatements
  "Pepsodent ger vitare tänder."
  "Pepsodent förbättrar din munhälsa."
  "Colgate motverkar plack och tandsten."                         # should have more...

table coverAdjectives
  "Pepsodent." "Gillar inte"  "Gillar"
  "Pepsodent." "Negativ" "Positiv"
  "Colgate." "Dålig"     "Bra"

#
# tasks
# -------

task showIntro
  show text "Vi undersöker hur attityder skapas till nya varumärken i förhållande till existerande."   0 -80
  show text "Varje del av studien kommer ha instruktioner. Vänligen läs"                0   0
  show text "instruktionerna noga och gör ditt bästa för att följa dem. " 0  40
  show text "studien att ta ca. 5 min."                 0  80
  show text "Tack för din medverkan!"                 0  120
  readmouse l 1 60000

task elabInstr
  table elabInstr
  font arial
  show text "(Klicka för att fortsätta)"       0 200
  set &elabScenario TABLEROW                              # 1=high, 2=low
  if &elabScenario == 1
    show text "Läs påståendena på nästa sida"                 0   0
    show text "Räkna vokalerna på varje rad"                  0  40
    show text "Multiplicera antalet med numret på samma rad"  0  80
    show text "Summera alla tal. Skriv inte ner något"        0 120
  fi
  if &elabScenario == 2
    show text "Läs påståendena på nästa skärm"  0   0
    show text "Var uppmärksam på tankar du har till vad som står skrivet"    0   40
    show text "Du får ta hur lång tid du behöver"    0  80
  fi
  readmouse l 1 60000

task coverInstr
  font arial
  show text "Svara på följande påståenden så snabbt som möjligt."  0   0
  delay 5000

task dentolInstr
  font arial
  show text "Svara på följande påståenden så snabbt som möjligt."  0   0
  delay 5000

task thankYouMessage
  font arial
  show text "Tack för din medverkan!"  0   0
  delay 2000

task dentolMessage
  font arial
  show text "Dentol tandkräm."                 0 -80
  show text "Bekämpar tandköttsinflammation."  0   0
  show text "Ger en bättre andedräkt."         0  40
  show text "Förebygger karies."               0  80
  show text "(Klicka för att fortsätta)"       0 200
  readmouse l 1 60000

task coverStatements
  table coverStatements
  clear screen
  mouse show
  show bitmap selector           -100  40
  show bitmap selector            100  40
  show text @1                      0 -80
  show text "Falskt"             -100  40
  show text "Sant"                100  40
  readmouse l 1 10000                                   # l=left mouse button, 1=first stiumli correct, 5000=wait for 5secs
  set $mouseX MOUSE_X                                   # current mouse x-coordinate
  set $mouseY MOUSE_Y                                   # current mouse x-coordinate
  set &falseTrue bitmap-under-mouse $mouseX $mouseY     # 1=Fasle, 2=True
  set &reactTime RT

task coverAdjectives
  table coverAdjectives
  clear screen
  mouse show
  show bitmap selector           -100  40
  show bitmap selector            100  40
  show text @1                      0 -80
  show text @2                   -100  40
  show text @3                    100  40
  readmouse l 1 10000
  set $mouseX MOUSE_X
  set $mouseY MOUSE_Y
  set &dislikeLike bitmap-under-mouse $mouseX $mouseY
  set &reactTime RT

task dentolStatements
  table dentolStatements
  clear screen
  mouse show
  show bitmap selector           -100  40
  show bitmap selector            100  40
  show text @1                      0 -80
  show text "Falskt"             -100  40
  show text "Sant"                100  40
  readmouse l 1 10000
  set $mouseX MOUSE_X
  set $mouseY MOUSE_Y
  set &dislikeLike bitmap-under-mouse $mouseX $mouseY
  set &reactTime RT
  save &elabScenario TASKNAME TABLEROW TRIALCOUNT RT &dislikeLike

task dentolAdjectives
  table dentolAdjectives
  clear screen
  mouse show
  show bitmap selector           -100  40
  show bitmap selector            100  40
  show text @1                      0 -80
  show text @2                   -100  40
  show text @3                    100  40
  readmouse l 1 10000
  set $mouseX MOUSE_X
  set $mouseY MOUSE_Y
  set &falseTrue bitmap-under-mouse $mouseX $mouseY
  set &reactTime RT
  save &elabScenario TASKNAME TABLEROW TRIALCOUNT RT &falseTrue

#
# blocks
# -------

block intro
  tasklist
    showIntro 1
  end

block instruction
  tasklist
    elabInstr 1
  end

block dentolMessag
  tasklist
    dentolMessage 1
  end

block coverInstr
  tasklist
    coverInstr 1
  end

block coverStatementsAdjectives
  tasklist
    coverStatements 3 fixed
    coverAdjectives 3 fixed
  end

block dentolInstr
  tasklist
    dentolInstr 1
  end

block dentolStatements
  tasklist
    dentolStatements 3 fixed
  end

block dentolAdjectives
  tasklist
    dentolAdjectives 3 fixed
  end

block thankYouMessage
  tasklist
    thankYouMessage 1
  end
